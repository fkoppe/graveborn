plugins {
    id 'java'
    id 'application'
}

group 'com.grave'
version '1.0'

application {
    applicationName = 'graveborn'
    mainClass = 'com.grave.Graveborn'
    // Mac OS with LWJGL 3 needs to be started with this JVM argument
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

project.ext {
  jmeVer = '3.6.1-stable'
}

project(":assets") {
    apply plugin: "java"

    buildDir = rootProject.file("build/assets")

    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        events("passed", "skipped", "failed")
    }
}

dependencies {

    // Core JME
    implementation "org.jmonkeyengine:jme3-core:$jmeVer"
    implementation "org.jmonkeyengine:jme3-desktop:$jmeVer"
    // Mac OS with LWJGL 3 doesn't allow AWT/Swing
    if (!System.getProperty("os.name").toLowerCase().contains("mac")) {
        implementation "org.jmonkeyengine:jme3-awt-dialogs:$jmeVer"
    }
    implementation "org.jmonkeyengine:jme3-lwjgl3:$jmeVer"

    // Suppress errors / warnings building in SDK
    implementation "org.jmonkeyengine:jme3-jogg:$jmeVer"
    implementation "org.jmonkeyengine:jme3-plugins:$jmeVer"

    // GUI Library
    //implementation "org.jmonkeyengine:jme3-niftygui:$jmeVer"

    // Physics Library
    //implementation "org.jmonkeyengine:jme3-jbullet:$jmeVer"

    // Networking Library
    implementation "org.jmonkeyengine:jme3-networking:$jmeVer"

    // Additional Libraries
    //implementation "org.jmonkeyengine:jme3-effects:$jmeVer"
    //implementation "org.jmonkeyengine:jme3-terrain:$jmeVer"
    //implementation "com.github.stephengold:Heart:9.1.0"
    //implementation "com.epagagames:particlemonkey:1.1.0"
    //implementation "com.simsilica:sio2:1.8.0"
    //implementation "com.simsilica:zay-es:1.5.0"
    //implementation "com.simsilica:zay-es-net:1.5.2"

    // Assets sub-project
    runtimeOnly project(':assets')


    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

wrapper {
    gradleVersion = '8.4'
}
